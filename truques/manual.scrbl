#lang scribble/doc

@;{
Copyright 2012,2013 Andrei Mikhailov

This file is part of bystroTeX.

bystroTeX is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

bystroTeX is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with bystroTeX.  If not, see <http://www.gnu.org/licenses/>.
}

@(require scribble/manual
          scribble/extract
          (for-label racket
                     scribble/core
                     scribble/base
                     scribble/html-properties
                     scribble/decode
                     scriblib/render-cond
                     (prefix-in the: xml)
                     xml/path
                     json
                     yaml
                     bystroTeX/common
                     "truques.rkt"
                     "dhall.rkt"
                     "xml.rkt"
                     "sqlite.rkt"
                     "terminal.rkt"
                     "yaml.rkt"
                     ))
@(require bystroTeX/common  "truques.rkt")

@title{BystroTeX truques}

Here I will write up various tricks useful with Scribble. 


@section{Truques}
@defmodule[truques/truques]

@defform[(explain expr ...)]{
First prints the code, then executes it an prints the result of the execution.
}

@defproc[
(show-and-go
 [a namespace-anchor?]
 [#:rest xs (listof string?)] 
) 
block?
]{
@bold{Deprecated}, better use @racket{(explain xs ...)}.
First prints the code, then executes it an prints the result of the execution. But first need to set up 
a namespace anchor, for example:
@verb|--{
@(define-namespace-anchor a)
@show-and-go[a]|-{
(format "Conversion rate: ~s" (~r #:precision '(= 4) dollar2real))
}-|
}--|
}

@defproc[
(curdir) element?
]{
Inserts link to the current dir 
}

@defproc[
(mailto [#:rest ems (listof string?)])
element?
]{Inserts the @tt{mailto:} link}

@defproc[
(autolist [#:exts list-of-extensions (listof symbol?) '(pdf)]
          [#:dir dir path-string? (get-bystro-scrbl-name)]
          [#:header header (or/c (listof any/c) #f) #f]
          [#:output o (-> path-string? (or/c (listof any/c)))
                     (λ (f)
                       `(,(hyperlink
                            (find-relative-path
                             (current-directory)
                             (path->complete-path (simplify-path (build-path dir f))))
                            (path->string f))))]
          [#:filter f (-> path-for-some-system? boolean?) (λ (p) #t)]
          )
(or/c table? element?)
]{Lists files with given extensions in the directory.
Each file is listed as a table row generated by @tt{f}. }

@defproc[
(autolist-pdfs [#:dir dir path-string? (build-path 'same)]
               [#:showtime st boolean? #f]
               [#:filter f (-> path-for-some-system? boolean?) (λ (p) #t)]
               [#:tags tgs (listof symbol?) '()])
(or/c table? element?)
]{
Lists PDF files in the directory. When @racket[tgs] is non-empty, only PDF
files whose accompanying @tt{.pdq} files contain the listed tags are shown.
}

@defproc[
(autolist-images [#:exts list-of-extensions (listof symbol?) '(svg png tiff jpg jpeg)]
                 [#:dir dir path-string? (build-path 'same)]
                 [#:scale scale number? 0.25]
                 [#:ncols number-of-columns integer? 2]
                 [#:filter filt (-> path-for-some-system? boolean?) (λ (p) #t)]
                 [#:showtime st boolean? #f]
                 [#:showdir sd boolean? #t]
                 [#:output o (path-string?  path? . -> . block?) (λ (d f)
                       (tbl `(,@`((,(hyperlink
                                     (build-path d f)
                                     (image #:scale scale (build-path d f))))
                                  (,(path->string f)))
                              ,@(if st
                                    `((,(date->string
                                         (seconds->date
                                          (file-or-directory-modify-seconds
                                           (find-relative-path
                                            (current-directory)
                                            (path->complete-path (simplify-path (build-path d f))))))))
                                    '())))))]
                 )
(or/c nested-flow? element?)
]{
List image files in the directory
}
             
@defproc[
(autolist-svgs
 [#:dir dir path-string? (build-path 'same)]
 [#:scale scale number? 0.25]
 [#:ncols number-of-columns integer? 2]
 [#:filter filt (-> path-for-some-system? boolean?) (λ (p) #t)]
 [#:showtime st boolean? #f]
 [#:showdir sd boolean? #t]
 [#:annotated annot boolean? #f]
 )
(or/c nested-flow? element?)
]{
List SVG files in the directory.
When @racket[annotated] is @racket[#t], scans SVG files for the
@tt{<desc>} tag and collects, on top of the table, the links to the corresponding images. In @bold{Inkscape}, the @tt{desc} can be inserted by going to @tt{Object properties}
and filling the @tt{Description} line. This is useful for creating a ``tag cloud''.
}

@defproc[
(autopart
 [title content?]
 [top (or/c block? (listof block?))]
 [#:tag tg string?]
 [#:subparts subs (listof part?)]
 [#:style stl (or/c style? #f) #f]
 [#:tag-prefix tagpref (or/c #f string? hash?) #f])
pre-part?
]{
Constructs a @racket[pre-part?] representing a Scribble @racket[part] whose
title is @racket[title], whose body blocks are given by @racket[top], and whose
immediate substructure is @racket[subs].

The required @racket[#:tag] argument @racket[tg] is converted to a section tag
(using @racket[make-section-tag]) and attached to the resulting part; this is
typically used for cross-references and stable anchors.

The @racket[#:style] argument controls the part style. If @racket[stl] is @racket[#f],
a default style is used.

The @racket[#:tag-prefix] argument is passed as the part’s tag prefix (the first
argument of @racket[make-part]). This is useful when generating families of parts
whose tags should share a prefix or be scoped in a specific way.

The @racket[top] argument may be either a single block or a list of blocks; in the
former case it is wrapped into a singleton list.
}

@defproc[
(autopage
 [title content?]
 [top (or/c block? (listof block?))]
 [#:showtitle sttl boolean? #t]
 [#:tag tg (or/c symbol? string? #f) #f]
 [#:subparts subs (listof pre-part?) '()])
pre-part?
]{
Constructs a @racket[pre-part?] page using @racket[page], followed by the body
blocks @racket[top] and then the additional @racket[pre-part?] values in
@racket[subs].

Example:
@(racketblock0
@(autopage
  #:tag "sample-autopage"
  @elem{Sample autopart}
  `(
    @,bystro-local-toc[]
    @,para{First paragraph of content}
    @,para{Second paragraph}
    )
  #:subparts
  `(
    @,subpage[1 "First subpage" #:tag "sec:FirstSubpage"]
    @,para{This is the first paragraph of the first subpage}
    @,para{This is the second paragraph of the first subpage}
    @,subpage[1 "Second subpage" #:tag "sec:SecondSubpage"]
    @,para{This is the first paragraph of the second subpage}
    @,para{THis is the second paragraph of the second subpage}
    )
  )
  )

}

@defproc[
(autosubpage
 [level integer?]
 [title content?]
 [top (or/c block? (listof block?))]
 [#:tag tg (or/c symbol? string? #f) #f]
 [#:subparts subs (listof pre-part?) '()])
pre-part?
]{
Constructs a @racket[pre-part?] page at an explicit section depth @racket[level]
(using @racket[(subpage level title ...)]), followed by the body blocks @racket[top]
and then the additional @racket[pre-part?] values in @racket[subs].
}



@section{Text}

@defproc[
(copy-to-clipboard
 [#:rows rows (or/c integer? #f) #f]
 [#:cols cols (or/c integer? #f) #f]
 [#:rest xs (listof string?)])
element?
]{
Content is copied to clipboard on pressing the button
}

@defproc[
(check [#:rest xs (listof any/c)])
(element?)
]{Checkbox labelled by the text}

@subsection{Nested styles}
@verb|---{
@nested[#:style @(make-style "comment" '()) @nested[#:style @(make-style "greenbox" '()) @verb|-{ ... @verb}-|
}---|

@subsection{Align to right}
@(define-namespace-anchor b)
@verb|--{
(tg table #:attrs ([border "0"] [width "100%"])
    (tg tr (tg td #:attrs ([style "text-align:right;"])
               "First line"
               (linebreak)
               "Second line"
               )))
}--|
@(tg table #:attrs ([border "0"] [width "100%"])
    (tg tr (tg td #:attrs ([style "text-align:right;"])
               "First line"
               (linebreak)
               "Second line"
               )))


@section{Format}
This prints @tt{x} to four decimal places:
@verb|--{
@(require racket/format)
(~r #:precision '(= 4) x)
}--|

@section{YAML}

@defmodule[truques/yaml]

@defproc[
 (yaml-to-clips
  [y yaml?]
  [#:rows rows (or/c integer? #f) 1]
  [#:cols cols (or/c integer? #f) 50])
 block?]{
Converts YAML data into Scribble output, rendering collections as tables or lists
and scalars as @racket[copy-to-clipboard] fields for easy reuse.
}

@section{XML}
@defmodule[truques/xml]


@defproc[
(xexpr-element? [v any/c])
boolean?
]{
Returns @racket[#t] if @racket[v] is an XML element node represented as an
@racket[the:xexpr], that is, a nonempty list whose first element is a symbol
(tag name).

This predicate is intended for use when traversing or filtering XML trees
represented as x-expressions.
}

@defproc[
(xexpr-select
 [xe the:xexpr?]
 [pred (-> any/c any/c)])
(listof the:xexpr?)
]{
Traverses the XML tree @racket[xe] and returns all element nodes for which
@racket[pred] returns a true value.

The predicate is applied only to XML element nodes (not to text nodes or
attributes). The traversal is performed in document order.

This function provides a generic selection mechanism underlying
@racket[xexpr-elements] and related helpers.
}

@defproc[
(xexpr-elements
 [tag symbol?]
 [xe the:xexpr?])
(listof the:xexpr?)
]{
Returns all XML element nodes in @racket[xe] whose tag name is equal to
@racket[tag].

Traversal is recursive and proceeds in document order. Only element nodes
whose first component is exactly @racket[tag] are returned.

This function is a convenience wrapper around @racket[xexpr-select].
}

@defproc[
(xexpr-elements*
 [tags (listof symbol?)]
 [xe the:xexpr?])
(listof the:xexpr?)
]{
Returns all XML element nodes in @racket[xe] whose tag name is a member of
@racket[tags].

Traversal is recursive and proceeds in document order. Membership is tested
using @racket[eq?] on tag symbols.

This function is useful when selecting multiple kinds of XML elements in a
single pass.
}
        

@defproc[
(file->xexpr [a path-string?]) 
the:xexpr?
]{Reads the @racket[the:xexpr] from the XML file}

@defproc[
(xexprs->nested-flow [xs (listof the:xexpr?)] [#:style s any/c #f])
nested-flow?
]{concatenates the values of XML nodes into a nested flow}

@verb|--{
@(let ([x (file->xexpr  "FILENAME.xml")])
   (tbl #:orient 'hor
        (for/list ([p (se-path*/list '(people) x)] #:when (cons? p)) 
          `(,(se-path* '(person #:nick) p)
            ,(xexprs->nested-flow #:style (bystro-elemstyle "background-color: LightCyan;") (se-path*/list '(person) p))))))
}--|

Why did not we write simply:
@verb|--{
(se-path* '(person) p)  (WRONG!)  ?
}--|
Because sometimes we have: @tt|--{ <person>&amp;john</person> }--| (can we exclude that a person's name starts with an ampersand?)

In XML, to insert the newline use @tt{&#a;}, and to insert the space use @tt{&#a0;}.

@defparam[
transform-to-content h (hash/c symbol? (-> the:xexpr? content?))]{
A parameter that defines the currently default transformers to content argument in @racket[show-xexpr]
}

@defparam[
transform-to-block h (hash/c symbol? (-> the:xexpr? block?))]{
A parameter that defines the currently default transformers to block argument in @racket[show-xexpr]
}

@defproc[
(show-xexpr [x the:xexpr?] 
            [#:transform-to-content t (hash/c symbol? (-> the:xexpr? content?)) (transform-to-content)]
            [#:transform-to-block tblock (hash/c symbol? (-> the:xexpr? block?)) (transform-to-block)]
            [#:show-root sr boolean? #f]
            [#:size size (or/c integer? #f) (show-xexpr-size)]
            [#:size-step step number? (show-xexpr-size-step)]
            [#:steps steps integer? (show-xexpr-steps)])
(or/c #f content? block?)
]{
Show XML data as a table. Here @racket[size] is the size of the root, and 
@racket[steps] is the  number of size-decreasing steps before size stabilization.

Example:
@verb|--{
@(define (f-transformer e) (f+0-2 (se-path* '(f) e)))
@(define (e-transformer email) 
   (hyperlink 
    (string-append "mailto:" (se-path* '(email) email)) 
    (se-path* '(email) email)))
@(parameterize
     ([transform-to-content 
       (hash-set* (transform-to-content) 
                  'f f-transformer 
                  'email e-transformer)])
   (show-xexpr some-xexpr))
}--|
}


@defproc[
($->_ [x string?]) 
string?
]{
Replaces dollar sign with underscore. Useful for SQLITE queries.
}

@include-extracted[truques/xml]


@section{SQLite tables}
@defmodule[truques/sqlite]

Start with defining the database connection:

@verb|--{
@(define conn (sqlite3-connect #:database "/path/to/your/database.sqlite" #:mode 'read-only))
}--|

And in the end do not forget to disconnect:

@verb|--{
@(disconnect conn)
}--|

@defproc[
(mysqli-tables 
 [conn connection?] 
 [#:column-titles column-titles (listof string?)]
 [#:sql x string?] 
 [#:params params (listof any/c) '()]
 [#:css css-file path-string?]
 [#:to-highlight to-hl (listof string?) '()]
 [#:to-hide  to-hide     (listof string?) '()])
(listof block?)]{
Prints out an SQLite database. The parameters @racket[to-highlight] and @racket[to-hide] are column titles as specified in 
@racket[column-titles]. Notice that @racket[column-titles] do not have to be same as actual names of columns in the database.
They are supposed to be ``titles'' for nice printing. Their assignment to the column names are by order (in the query).
}

@defproc[
(get-column-names
 [conn connection?] 
 [#:table t string?])
 (listof string?)]{

Get the list of the column names. Sample use:
@verb|--{
@(mysqli-tables
  conn
  #:column-titles (get-column-names conn #:table "mytable")
  #:sql "select * from mytable where name like ?"
  #:params '("Andrei")
  #:css "my-tables-style.css")
}--|
}

@section{Terminal}
@defmodule[truques/terminal]

@defproc[(stty-minus-f-arg-string) string?]{returns "-F" for Linux and "-f" for Mac}
@defproc[(askpass) string?]{reads password from the console, without echo}
@defproc[(get-one-char) char?]{waits for keypress}
@defproc[(ansi-off) string?]{gives "\\033[0m"}
@defproc[(ansi-fg256 [rgb integer?] [x string?]) string?]{256-color terminal, 
see @hyperlink["https://en.wikipedia.org/wiki/File:Xterm_256color_chart.svg"]{Xterm_256color_chart.svg}}
@defproc[(ansi-bg256 [rgb integer?] [x string?]) string?]{256-color terminal}
@defproc[(ansi-bold [x string?]) string?]{bold}
@defproc[(ansi-underline [x string?]) string?]{underlined}
@defproc[(ansi-blink [x string?]) string?]{blinking}
@defproc[(ansi-reverse [x string?]) string?]{reverse}
@defproc[(ansi-clear-screen) any/c]{clear screen}

@section{Dhall}

@defmodule[truques/dhall]

@defproc[
 (dhall
  [#:dir dir (or/c path-string? #f) #f]
  [#:output output-type (or/c 'json 'yaml 'type 'dhall) 'yaml]
  [#:yaml-printer yaml-printer (or/c #f (-> yaml? block?)) #f]
  [#:rest code (listof string?)])
 block?]{
Evaluates Dhall source by invoking the system executables (such as
@tt{dhall-to-yaml}) in an optional working directory. The result is returned as
a Scribble block, and YAML output can be post-processed with
@racket[yaml-printer].
}

@section{Multishow}

@defmodule[truques/multishow]

@defproc[
         (multishow
          [#:input input-type (or/c 'dhall 'ncl 'nu)]
          [#:rest code (listof string?)]
          [#:file input-file (or/c #f path-string?) #f]
          [#:dir dir (or/c #f path-string?) #f]
          [#:output output-type (or/c 'text 'json 'yaml 'type 'dhall 'toml) 'yaml]
          [#:yaml-printer yaml-printer (or/c #f (-> yaml? block?)) #f]
          [#:json-printer json-printer (or/c #f (-> jsexpr? block?)) show-json]
          )
         block?]{
                 Inserts contents of a @hyperlink["https://dhall-lang.org/"]{Dhall}
                 or @hyperlink["https://nickel-lang.org/"]{Nickel file}, or
                 an output from @hyperlink["https://www.nushell.sh/"]{NuShell}. Example:
                 @verb|{                 
                        @multishow[
                                   #:input 'nu
                                   #:output 'text
                                   #:dir "/home/andrei/"
                                   ]{
                                     ls | table | ansi strip
                                     }
                        }|
                 }


@defproc[
         (show-json
          [jsn jsexpr?]
          [#:font-size-step step string? "80%"])
         block?]{
                 Inserts prettified JSON
                 }

@section{DOCX}

@defmodule[truques/docx]

@include-extracted[truques/docx]

@section{Tailwind CSS}

@defmodule[truques/tailwind]

@include-extracted[truques/tailwind]

@section{PdQ}

@defmodule[truques/pdq]

@include-extracted[truques/pdq]

@section{Markdown}

@defmodule[truques/md]

@include-extracted[truques/md]

@section{Directory traversal}

@defmodule[truques/dir-traverse]

@include-extracted[truques/dir-traverse]


@section{Legal}

Copyright 2012,2013,2014 Andrei Mikhailov

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
